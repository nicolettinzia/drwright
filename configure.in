AC_INIT(src)
AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(drwright, 0.18)

AM_MAINTAINER_MODE

AC_ISC_POSIX
AC_PROG_CC
AC_STDC_HEADERS
AM_PROG_LIBTOOL
AC_PATH_XTRA
AM_PATH_GLIB_2_0

AC_PROG_INTLTOOL([0.29])

dnl ==========================================================================
dnl
dnl Library Dependencies, uses pkg-config to detect correct version, etc
dnl
dnl ==========================================================================

dnl If you add a version number here, you *must* add an AC_SUBST line for
dnl it too, or it will never make it into the spec file!

GLIB_REQUIRED=2.2.0
PANGO_REQUIRED=1.2
GTK_REQUIRED=2.2.0
GCONF_REQUIRED=1.2.0
LIBGLADE_REQUIRED=2.0.0
LIBGNOMEUI_REQUIRED=2.0.0
DESKTOP_FILE_UTILS_REQUIRED=0.2.90

AC_SUBST(GLIB_REQUIRED)
AC_SUBST(PANGO_REQUIRED)
AC_SUBST(GTK_REQUIRED)
AC_SUBST(GCONF_REQUIRED)
AC_SUBST(LIBGLADE_REQUIRED)
AC_SUBST(LIBGNOMEUI_REQUIRED)
AC_SUBST(DESKTOP_FILE_UTILS_REQUIRED)

dnl D-BUS
dnl ==========================
AC_ARG_ENABLE(dbus, [  --enable-dbus=[no/yes/auto]    compile with d-bus support.],,enable_dbus=auto)

if test "x$enable_dbus" != "xno"; then
   dbus_pkgconfig="dbus-1 >= 0.50 dbus-glib-1"
   PKG_CHECK_MODULES(DBUS, $dbus_pkgconfig, have_dbus=yes, have_dbus=no)
   if test x$have_dbus = xyes; then
      AC_DEFINE(HAVE_DBUS, 1, d-bus support)
   fi
else
   have_dbus=no
   dbus_pkgconfig=
fi

if test "x$have_dbus" != "xyes"; then
  if test "x$enable_dbus" = "xyes"; then
      AC_MSG_ERROR([Couldn't find d-bus.])
  else
   dbus_pkgconfig=
  fi
fi

AM_CONDITIONAL(HAVE_DBUS, test x$have_dbus = xyes)

dnl ================================
dnl | Pkg-Config dependency checks |---------------------------
dnl ================================

PKG_CHECK_MODULES(DRWRIGHT,
[
	glib-2.0 >= $GLIB_REQUIRED
	gobject-2.0
	gtk+-2.0 >= $GTK_REQUIRED
	gconf-2.0 >= $GCONF_REQUIRED
	libglade-2.0 >= $LIBGLADE_REQUIRED 
	libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED 	
	$dbus_pkgconfig
])

AC_SUBST(DRWRIGHT_CFLAGS)
AC_SUBST(DRWRIGHT_LIBS)
dnl -----------------------------------------------------------

dnl
dnl Check for XScreenSaver extension
dnl		

dnl Check for XScreenSaver
old_LIBS="$LIBS"
old_CFLAGS="$CFLAGS"
LIBS="$LIBS -L$x_libraries"
CFLAGS="$CFLAGS -I$x_includes"
AC_CHECK_LIB(Xext, XScreenSaverQueryInfo,[XSS_LIBS="-L$x_libraries"],[],[-lX11 -lXext -lm])
AC_CHECK_LIB(Xss, XScreenSaverQueryInfo,[XSS_LIBS="-L$x_libraries -lXss"],[],[-lX11 -lXext -lm])
LIBS="$old_LIBS"
CFLAGS="$old_CFLAGS"
if test "x$XSS_LIBS" = "x"; then
  AC_MSG_ERROR([XScreenSaver extension not found])  
fi
AC_SUBST(XSS_LIBS)

dnl  ------------------
dnl | Language Support |---------------------------------------
dnl  ------------------
GETTEXT_PACKAGE=drwright
AC_SUBST(GETTEXT_PACKAGE)
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", "")

ALL_LINGUAS="ar az bg ca cs cy da de dz el en_CA en_GB es fi fr gu hr is it ja ko ml mn ms nb ne nl pl pt pt_BR ru rw sk sq sr sr@Latn sv tr uk vi wa zh_CN zh_TW"

AM_GLIB_GNU_GETTEXT

if test "x$prefix" = "xNONE"; then
  DRWRIGHT_LOCALEDIR=$ac_default_prefix/share/locale
else
  DRWRIGHT_LOCALEDIR=$prefix/share/locale
fi
AC_DEFINE_UNQUOTED(DRWRIGHT_LOCALEDIR, "$DRWRIGHT_LOCALEDIR", "")
dnl -----------------------------------------------------------

AC_PATH_PROG(GCONFTOOL, gconftool-2, no)

if test x"$GCONFTOOL" = xno; then
  AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi

AM_GCONF_SOURCE_2

AC_OUTPUT([
Makefile
src/Makefile
po/Makefile.in
data/Makefile
data/images/Makefile
data/glade/Makefile
drwright.spec
])

echo
echo D-BUS support: $have_dbus
echo
