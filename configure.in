# This programme is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public
# License as published by the Free Software Foundation; either
# version 2 of the License, or (at your option) any later version.
#
# This programme is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
#
# You should have received a copy of the GNU General Public
# License along with this programme; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA

AC_INIT([Typing Break],
        [3.5.0],
        [http://bugzilla.gnome.org/enter_bug.cgi?product=drwright],
        [drwright])

AC_CONFIG_SRCDIR([src/main.c])
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE([1.9 foreign no-dist-gzip dist-bzip2 -Wno-portability])

m4_ifdef([AM_SILENT_RULES],[AM_SILENT_RULES([yes])])

if test -z "$enable_maintainer_mode"; then
  enable_maintainer_mode=yes
fi
AM_MAINTAINER_MODE

AC_STDC_HEADERS
AC_PROG_CC
AM_PROG_CC_C_O
AC_HEADER_STDC
PKG_PROG_PKG_CONFIG

LT_INIT([disable-static])
LT_LIB_M

# ************
# Requirements
# ************

GLIB_REQUIRED=2.31.13
GIO_REQUIRED=2.31.13
GDK_PIXBUF_REQUIRED=2.25.3
GTK_REQUIRED=3.0.0
LIBCANBERRA_REQUIRED=0
LIBNOTIFY_REQUIRED=0.7
GSD_REQUIRED=2.91.8

# ************
# Typing Break
# ************

PKG_CHECK_MODULES([TYPING],[
  glib-2.0 >= $GLIB_REQUIRED
  gio-2.0 >= $GIO_REQUIRED
  gdk-pixbuf-2.0 >= $GDK_PIXBUF_REQUIRED
  gtk+-3.0 >= $GTK_REQUIRED
  libcanberra-gtk3 >= $LIBCANBERRA_REQUIRED
  libnotify >= $LIBNOTIFY_REQUIRED
  x11])

AC_PATH_XTRA
x_libs="$X_PRE_LIBS $X_LIBS -lX11 $X_EXTRA_LIBS"

SCREENSAVER_LIBS=
AC_CHECK_FUNC(XScreenSaverQueryInfo, [], [
  save_LIBS=$LIBS
  LIBS="$LIBS $X_PRE_LIBS $X_LIBS -lXss -lXext -lX11"
  AC_CHECK_LIB(Xss, XScreenSaverQueryInfo, [
    SCREENSAVER_LIBS="$X_PRE_LIBS $X_LIBS -lXss -lXext -lX11"], [
    LIBS=$save_LIBS
    LIBS="$LIBS $X_PRE_LIBS $X_LIBS -lXext -lX11"
    AC_CHECK_LIB(Xext, XScreenSaverQueryInfo, [
      SCREENSAVER_LIBS="$X_PRE_LIBS $X_LIBS -lXext -lX11"],[])])
  LIBS=$save_LIBS])
AC_SUBST(SCREENSAVER_LIBS)

# **********************
# Settings daemon plugin
# **********************

PKG_CHECK_MODULES([GSD_PLUGIN],[
  gnome-settings-daemon >= $GSD_REQUIRED
  gio-2.0 >= $GIO_REQUIRED
  gtk+-3.0
  gdk-x11-3.0])

gsd_plugindir='${libdir}/gnome-settings-daemon-3.0'
AC_SUBST([gsd_plugindir])

GSD_INTLTOOL_PLUGIN_RULE='%.gnome-settings-plugin:   %.gnome-settings-plugin.in   $(INTLTOOL_MERGE) $(wildcard $(top_srcdir)/po/*.po) ; LC_ALL=C $(INTLTOOL_MERGE) -d -u -c $(top_builddir)/po/.intltool-merge-cache $(top_srcdir)/po $< [$]@'
AC_SUBST([GSD_INTLTOOL_PLUGIN_RULE])

# ********************
# Control centre panel
# ********************

PKG_CHECK_MODULES([CCPANEL],[gio-2.0 gtk+-3.0])

ccpanelsdir='${libdir}/control-center-1/panels'
AC_SUBST([ccpanelsdir])

AC_PATH_PROG([GTK_BUILDER_CONVERT],[gtk-builder-convert],[false])
if test "$GTK_BUILDER_CONVERT" = "false"; then
  AC_MSG_ERROR([gtk-builder-convert not found])
fi

AC_ARG_VAR([GLIB_COMPILE_RESOURCES],[the glib-compile-resources programme])
AC_PATH_PROG([GLIB_COMPILE_RESOURCES],[glib-compile-resources],[])
if test -z "$GLIB_COMPILE_RESOURCES"; then
  AC_MSG_ERROR([glib-compile-resources not found])
fi

AC_ARG_VAR([XMLLINT],[the xmllint programme])
AC_PATH_PROG([XMLLINT],[xmllint],[])
if test -z "$XMLLINT"; then
  AC_MSG_ERROR([xmllint not found])
fi

# *********
# GSettings
# *********

GLIB_GSETTINGS

# ****
# i18n
# ****

GETTEXT_PACKAGE=drwright
AC_SUBST([GETTEXT_PACKAGE])
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE],["$GETTEXT_PACKAGE"],[Gettext package])
AM_GLIB_GNU_GETTEXT
IT_PROG_INTLTOOL([0.35.0])

# ******************************************************************************
# ******************************************************************************

AC_CONFIG_FILES([
Makefile
src/Makefile
src/gnome-typing-break-panel.desktop.in:src/gnome-typing-break-panel.desktop.in.in
po/Makefile.in
])

AC_OUTPUT
